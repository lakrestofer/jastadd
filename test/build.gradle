apply plugin: 'java'

defaultTasks 'test'

repositories {
	mavenCentral()
}

sourceSets {
	test {
		java {
			compileClasspath += files('../jastadd2.jar')
			runtimeClasspath += files('../jastadd2.jar')
			srcDirs 'src/core', 'src/tests'
		}
	}
}

dependencies {
	testImplementation 'junit:junit:4.12'
}

test {
	dependsOn 'cleanTest'

	def testSuite = System.getProperty('testSuite', 'tests.jastadd2.TestShouldPass')
	def testSuitePath = testSuite.replace('.', '/') + '.class'
	include testSuitePath
	include 'tests/jastadd2/analysis/*.class'

	systemProperty 'debug_jastadd', System.getProperty('jastadd_debug', 'false')
	systemProperty 'jastadd.jar', System.getProperty('jastadd.jar', '../jastadd2.jar')
	systemProperty 'test', System.getProperty('test', '')
	systemProperty 'org.jastadd.testTimeout', 5000
	systemProperty 'org.jastadd.testThreads', 8
}
